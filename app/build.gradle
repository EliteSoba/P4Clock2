plugins {
  id 'com.android.application'
}
android {
  // Load keystore properties if file exists
  def keystoreProperties = file('./signing.properties')
  if (keystoreProperties.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(keystoreProperties))
    props.each { prop ->
      project.ext.set(prop.key, prop.value)
    }
  }

  // Read passwords from Gradle properties and if not found, then from environment variables
  def keystore_file = project.hasProperty('KEYSTORE_FILE') ? KEYSTORE_FILE : System.getenv("KEYSTORE_FILE")
  def keystore_password = project.hasProperty('KEYSTORE_PASSWORD') ? KEYSTORE_PASSWORD : System.getenv("KEYSTORE_PASSWORD")
  def key_alias = project.hasProperty('KEY_ALIAS') ? KEY_ALIAS : System.getenv("KEY_ALIAS")
  def key_password = project.hasProperty('KEY_PASSWORD') ? KEY_PASSWORD : System.getenv("KEY_PASSWORD")

  signingConfigs {
      release {
        storeFile file(keystore_file)
        storePassword keystore_password
        keyAlias key_alias
        keyPassword key_password
      }
  }
  compileSdk 32

  defaultConfig {
    applicationId "com.soba.persona.p4clockultimax"
    minSdk 26
    targetSdk 32
    versionCode 1
    versionName "1.1"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  buildFeatures {
    viewBinding true
  }
}

dependencies {
  implementation 'androidx.appcompat:appcompat:1.4.2'
  implementation 'com.google.android.material:material:1.6.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'com.google.android.gms:play-services-location:20.0.0'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}